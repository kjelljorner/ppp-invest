import pandas as pd
import itertools
from pathlib import Path
import os
import tarfile


def aggregate_input(wildcards):
    checkpoint_output = checkpoints.extract_xyz.get(**wildcards).output[0]
    return expand(
        "results/{name}/{sample}.csv",
        name=wildcards.name,
        sample=glob_wildcards(os.path.join(checkpoint_output, "{i}.xyz")).i,
    )


NAMES = ["azaphenalenes", "azulenes", "rational_design"]


rule all:
    input:
        expand(["results/{name}_combined.csv", "results/{name}.tar.gz"], name=NAMES),


rule combine_csvs:
    input:
        aggregate_input,
    output:
        csv="results/{name}_combined.csv",
        tar="results/{name}.tar.gz",
    conda:
        "envs/coulson.yml"
    shell:
        "csvstack {input} > {output.csv} && tar -cf {output.tar} {input}"


checkpoint extract_xyz:
    input:
        "data/xyz/{name}/xyz.tar.gz",
    output:
        directory("resources/xyz/{name}"),
    shell:
        "mkdir -p {output} && tar -xf {input} -C {output}"


rule calculate_compound:
    input:
        "resources/xyz/{name}/{sample}.xyz",
    output:
        "results/{name}/{sample}.csv",
    conda:
        "envs/coulson.yml"
    script:
        "scripts/calculate.py"
